apply plugin: project.PLATFORM == project.PLATFORM_NETTY ? project.PLUGIN_JAVA_LIB: project.PLUGIN_ANDROID_LIB

ext {
    bintrayRepo = 'maven'
    bintrayName = 'autobahn'

    publishedGroupId = 'io.crossbar'
    libraryName = 'autobahn'
    artifact = 'autobahn'

    libraryDescription = 'WebSocket and WAMP in Java'

    siteUrl = 'https://github.com/crossbario/autobahn-java'
    gitUrl = 'https://github.com/crossbario/autobahn-java.git'

    libraryVersion = '17.9.2'

    developerId = 'crossbario'
    developerName = 'crossbario'
    developerEmail = 'support@crossbario.com'

    licenseName = 'MIT License'
    licenseUrl = 'http://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

if (plugins.hasPlugin(project.PLUGIN_ANDROID_LIB)) {
    android {
        compileSdkVersion 26
        buildToolsVersion '26.0.1'

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 26
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            }
        }

        sourceSets {
            main {
                java {
                    exclude 'io/crossbar/autobahn/wamp/transports/Netty*'
                }
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
} else {
    sourceSets {
        main {
            java {
                exclude 'io/crossbar/autobahn/wamp/transports/AndroidWebSocket.java'
                exclude 'io/crossbar/autobahn/websocket'
            }
        }
    }
    jar {
        version = project.properties.get("buildVersion", "")
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.8.8'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.8.13'
    // implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.5.6'
    if (plugins.hasPlugin(project.PLUGIN_JAVA_LIB)) {
        implementation 'io.netty:netty-codec-http:4.1.2.Final'
        implementation 'io.netty:netty-handler:4.1.2.Final'
        api 'com.fasterxml.jackson.core:jackson-core:2.8.8'
    }
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
