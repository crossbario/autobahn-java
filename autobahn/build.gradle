apply plugin: project.PLATFORM == project.PLATFORM_NETTY ? project.PLUGIN_JAVA_LIB: project.PLUGIN_ANDROID_LIB

if (plugins.hasPlugin(project.PLUGIN_ANDROID_LIB)) {
    android {
        compileSdkVersion 26
        buildToolsVersion '26.0.1'
        useLibrary 'org.apache.http.legacy'

        defaultConfig {
            minSdkVersion 24
            targetSdkVersion 26
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            }
        }

        sourceSets {
            main {
                java {
                    exclude 'io/crossbar/autobahn/wamp/transports/Netty*'
                }
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
    }
} else {
    sourceSets {
        main {
            java {
                include 'io/crossbar/autobahn/wamp/**'
                exclude 'io/crossbar/autobahn/wamp/transports/AutobahnTransport.java'
            }
        }
    }
    distributions {
        main {
            baseName = 'autobahn'
        }
    }
    jar {
        version = project.properties.get("buildVersion", "")
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.8'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.8.8'
    implementation 'org.msgpack:jackson-dataformat-msgpack:0.8.13'
    implementation 'io.netty:netty-codec-http:4.1.2.Final'
    implementation 'io.netty:netty-handler:4.1.2.Final'
    if (plugins.hasPlugin(project.PLUGIN_JAVA_LIB)) {
        runtime 'com.fasterxml.jackson.core:jackson-core:2.8.8'
        runtime 'com.fasterxml.jackson.core:jackson-databind:2.8.8'
        runtime 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.8.8'
        runtime 'org.msgpack:jackson-dataformat-msgpack:0.8.13'
    }
}
