// Set global platform properties.
def _PLATFORM = project.properties.get('buildPlatform')
assert (_PLATFORM == 'android' || _PLATFORM == 'netty' || _PLATFORM == 'next' || _PLATFORM == null)

def _PUBLISH = true
if (_PLATFORM == null) {
    _PLATFORM = 'android'
    _PUBLISH = false
}

project.ext {
    PLATFORM = _PLATFORM
    PLUGIN_ANDROID_APP = 'com.android.application'
    PLUGIN_JAVA_APP = 'application'
    IS_ANDROID = _PLATFORM == 'android'
    IS_NETTY = _PLATFORM == 'netty'
    IS_NEXT = _PLATFORM == 'next'
    BUILD_LEGACY = project.properties.get('buildLegacy', false)
    PUBLISH = _PUBLISH
}

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
    // Android specific dependencies.
    if (project.properties.get('buildPlatform', 'android') == 'android') {
        dependencies {
            classpath 'com.android.tools.build:gradle:4.0.1'
            classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        }
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/ethereum/maven/" }
    }
}
